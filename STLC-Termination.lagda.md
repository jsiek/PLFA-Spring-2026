```
{-# OPTIONS --rewriting #-}

module STLC-Termination where
```

# A proof that all STLC programs terminate

## Imports

```
import Syntax
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Data.List using (List; []; _âˆ·_)
open import Data.Maybe
open import Data.Nat using (â„•; zero; suc)
open import Data.Product using (_Ã—_; Î£; Î£-syntax; âˆƒ; âˆƒ-syntax; projâ‚; projâ‚‚)
   renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Unit using (âŠ¤; tt)
open import Relation.Binary.PropositionalEquality
 using (_â‰¡_; refl; sym; cong; congâ‚‚)

open import lecture-notes-Substitution
```

## Values

```
data Value : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Set where

  V-Æ› : âˆ€ {Î“ A B} {N : Î“ , A âŠ¢ B}
      ---------------------------
    â†’ Value (Æ› N)

  V-zero : âˆ€ {Î“}
      -----------------
    â†’ Value (`zero {Î“})
```

## Reduction

```
infix 2 _â€”â†’_

data _â€”â†’_ : âˆ€ {Î“ A} â†’ (Î“ âŠ¢ A) â†’ (Î“ âŠ¢ A) â†’ Set where

  Î¾-Â·â‚ : âˆ€ {Î“ A B} {L Lâ€² : Î“ âŠ¢ A â‡’ B} {M : Î“ âŠ¢ A}
    â†’ L â€”â†’ Lâ€²
      ---------------
    â†’ L Â· M â€”â†’ Lâ€² Â· M

  Î¾-Â·â‚‚ : âˆ€ {Î“ A B} {V : Î“ âŠ¢ A â‡’ B} {M Mâ€² : Î“ âŠ¢ A}
    â†’ Value V
    â†’ M â€”â†’ Mâ€²
      ---------------
    â†’ V Â· M â€”â†’ V Â· Mâ€²

  Î²-Æ› : âˆ€ {Î“ A B} {N : Î“ , A âŠ¢ B} {W : Î“ âŠ¢ A}
    â†’ Value W
      --------------------
    â†’ (Æ› N) Â· W â€”â†’ N [ W ]
```

## Multi-step reduction

```
infix  2 _â€”â† _
infix  1 begin_
infixr 2 _â€”â†’âŸ¨_âŸ©_
infix  3 _âˆ

data _â€”â† _ : âˆ€ {Î“ A} â†’ (Î“ âŠ¢ A) â†’ (Î“ âŠ¢ A) â†’ Set where

  _âˆ : âˆ€ {Î“ A} (M : Î“ âŠ¢ A)
      ------
    â†’ M â€”â†  M

  stepâ€”â†’ : âˆ€ {Î“ A} (L : Î“ âŠ¢ A) {M N : Î“ âŠ¢ A}
    â†’ M â€”â†  N
    â†’ L â€”â†’ M
      ------
    â†’ L â€”â†  N

pattern _â€”â†’âŸ¨_âŸ©_ L Lâ€”â†’M Mâ€”â† N = stepâ€”â†’ L Mâ€”â† N Lâ€”â†’M

begin_ : âˆ€ {Î“ A} {M N : Î“ âŠ¢ A}
  â†’ M â€”â†  N
    ------
  â†’ M â€”â†  N
begin Mâ€”â† N = Mâ€”â† N
```

```
â€”â† -trans : âˆ€{Î“}{A}{M N L : Î“ âŠ¢ A} â†’ M â€”â†  N â†’ N â€”â†  L â†’ M â€”â†  L
â€”â† -trans (M âˆ) Nâ€”â† L = Nâ€”â† L
â€”â† -trans (L â€”â†’âŸ¨ Lâ€”â†’M âŸ© Mâ€”â† N) Nâ€”â† L =
  let IH = â€”â† -trans Mâ€”â† N Nâ€”â† L in
  L â€”â†’âŸ¨ Lâ€”â†’M âŸ© IH

infixr 2 _â€”â† âŸ¨_âŸ©_
_â€”â† âŸ¨_âŸ©_ : âˆ€ {Î“}{A}(L : Î“ âŠ¢ A) {M N : Î“ âŠ¢ A}
    â†’ L â€”â†  M
    â†’ M â€”â†  N
      ------
    â†’ L â€”â†  N
L â€”â† âŸ¨ LM âŸ© MN = â€”â† -trans LM MN
```

## Termination via Logical Relations

We define a predicate `â„° A M` for terms that are well-behaved
according to type `A`. In particular, they are
terms that halt and produce a value, and furthermore the value must
behave according to its type `A`.  So the definition of â„° is mutually
recursive with another predicate `ğ’± A V` that specifies the well-behaved
values..

```
â„° : (A : Type) â†’ âˆ… âŠ¢ A â†’ Set
ğ’± : (A : Type) â†’ âˆ… âŠ¢ A â†’ Set

â„° A M = Î£[ V âˆˆ âˆ… âŠ¢ A ] (M â€”â†  V) Ã— (Value V) Ã— (ğ’± A V)
```

The predicate ğ’± for type â„• says the value must be a natural number, but
`ğ’± (A â‡’ B)` is more interesting. It says the value must be a
`Æ› N` where `N[ V ]` is a term that is well-behaved according to type `B`,
that is, `â„° B (N [ V ])` for any value `V` provided that it behaves
according to type `A`, that is, `ğ’± A V`.

```
ğ’± `â„• `zero = âŠ¤
ğ’± `â„• _ = âŠ¥
ğ’± (A â‡’ B) (Æ› N) = âˆ€ {V : âˆ… âŠ¢ A} â†’ ğ’± A V â†’ â„° B (N [ V ])
ğ’± (A â‡’ B) _ = âŠ¥
```

The terms in ğ’± are indeed values.

```
ğ’±â†’Value : âˆ€{A}{M : âˆ… âŠ¢ A} â†’ ğ’± A M â†’ Value M
ğ’±â†’Value {`â„•} {`zero} wtv = V-zero
ğ’±â†’Value {A â‡’ B} {Æ› N} wtv = V-Æ›
```

The ğ’± function implies the â„° function.
(A well-behaved value is a well-behaved term.)

```
ğ’±â†’â„° : âˆ€{A}{M : âˆ… âŠ¢ A} â†’ ğ’± A M â†’ â„° A M
ğ’±â†’â„° {A}{M} wtv = âŸ¨ M , âŸ¨ M âˆ , âŸ¨ ğ’±â†’Value {A} wtv , wtv âŸ© âŸ© âŸ©
```

### Canonical forms

```
ğ’±â‡’â†’Æ› : âˆ€{A}{B}{M : âˆ… âŠ¢ A â‡’ B} â†’ ğ’± (A â‡’ B) M â†’ Î£[ N âˆˆ âˆ… , A âŠ¢ B ] M â‰¡ Æ› N
ğ’±â‡’â†’Æ› {A}{B}{Æ› N} wtv = âŸ¨ N , refl âŸ©

data Natural : âˆ… âŠ¢ `â„• â†’ Set where
   Nat-Z : Natural (`zero)

ğ’±â„•â†’Nat : âˆ€{M : âˆ… âŠ¢ `â„•} â†’ ğ’± `â„• M â†’ Natural M
ğ’±â„•â†’Nat {`zero} wtv = Nat-Z
```

### Compatibility lemmas about reduction

```
app-compat : âˆ€{A}{B} {L L'  : âˆ… âŠ¢ A â‡’ B}{M M' : âˆ… âŠ¢ A}
           â†’ L â€”â†  L' â†’ Value L'
           â†’ M â€”â†  M'
           â†’ L Â· M â€”â†  L' Â· M'
app-compat {A}{B}{L}{L}{M}{M} (L âˆ) vL (M âˆ) = L Â· M âˆ
app-compat {A}{B}{L}{L}{M}{M''} (L âˆ) vL (stepâ€”â†’ M {M'} M'â†’M'' Mâ†’M') =
  begin
     L Â· M     â€”â†’âŸ¨ Î¾-Â·â‚‚ vL Mâ†’M' âŸ©
     L Â· M'    â€”â† âŸ¨ app-compat (L âˆ) vL M'â†’M'' âŸ©
     L Â· M''
  âˆ
app-compat {A}{B}{L}{L''}{M}{M'}(stepâ€”â†’ L {L'}{L''} L'â†’L'' Lâ†’L' ) vL' Mâ†’M' =
  begin
    L Â· M             â€”â†’âŸ¨ Î¾-Â·â‚ Lâ†’L' âŸ©
    L' Â· M            â€”â† âŸ¨ app-compat L'â†’L'' vL' Mâ†’M' âŸ©
    L'' Â· M'
  âˆ
```

### A technical lemma about extending substitutions

```
_âŠ¢Ë¢_ : (Î“ : Context) â†’ (Î“ â†’Ë¢ âˆ…) â†’ Set
Î“ âŠ¢Ë¢ Ïƒ = (âˆ€ {C : Type} (x : Î“ âˆ‹ C) â†’ ğ’± C (Ïƒ x))
```

```
extend-sub : âˆ€{A}{V : âˆ… âŠ¢ A}{Î“}{Ïƒ : Î“ â†’Ë¢ âˆ…}
         â†’ ğ’± A V   â†’   Î“ âŠ¢Ë¢ Ïƒ
         â†’ (Î“ , A) âŠ¢Ë¢ (V â€¢ Ïƒ)
extend-sub {A} {V} wtv âŠ¢Ïƒ {C} Z = wtv
extend-sub {A} {V} wtv âŠ¢Ïƒ {C} (S âˆ‹x) = âŠ¢Ïƒ âˆ‹x
```

### The fundemantal property of the logical relation

```
fundamental-property : âˆ€ {A}{Î“} {Ïƒ : Î“ â†’Ë¢ âˆ…}
  â†’ (M : Î“ âŠ¢ A)
  â†’ Î“ âŠ¢Ë¢ Ïƒ
  â†’ â„° A (subst Ïƒ M)
fundamental-property {A} (` âˆ‹x) âŠ¢Ïƒ = ğ’±â†’â„° {A} (âŠ¢Ïƒ âˆ‹x)

fundamental-property {A â‡’ B}{Î“}{Ïƒ} (Æ› N) âŠ¢Ïƒ =
   âŸ¨ subst Ïƒ (Æ› N) , âŸ¨ Æ› (subst (exts Ïƒ) N) âˆ , âŸ¨ V-Æ› , IH âŸ© âŸ© âŸ©
   where
   IH : {V : âˆ… âŠ¢ A} â†’ ğ’± A V â†’ â„° B (subst (exts Ïƒ) N [ V ])
   IH {V} wtv = fundamental-property {B}{Î“ , A}{V â€¢ Ïƒ} N (extend-sub wtv âŠ¢Ïƒ)

fundamental-property {B}{Î“}{Ïƒ} (_Â·_{A = A} L M) âŠ¢Ïƒ 
    with fundamental-property {A â‡’ B}{Î“}{Ïƒ} L âŠ¢Ïƒ
... | âŸ¨ L' , âŸ¨ Lâ†’L' , âŸ¨ vL' , wtvL' âŸ© âŸ© âŸ©
    with ğ’±â‡’â†’Æ› wtvL'
... | âŸ¨ N , eq âŸ© rewrite eq 
    with fundamental-property M âŠ¢Ïƒ
... | âŸ¨ M' , âŸ¨ Mâ†’M' , âŸ¨ vM' , wtvM' âŸ© âŸ© âŸ©
    with wtvL' wtvM'
... | âŸ¨ V , âŸ¨ â†’V , âŸ¨ vV , wtvV âŸ© âŸ© âŸ© =
      âŸ¨ V , âŸ¨ R , âŸ¨ vV , wtvV âŸ© âŸ© âŸ©
    where
    R : subst Ïƒ L Â· subst Ïƒ M â€”â†  V
    R =   begin
          subst Ïƒ L Â· subst Ïƒ M     â€”â† âŸ¨ app-compat Lâ†’L' vL' Mâ†’M' âŸ©
          (Æ› N) Â· M'                â€”â†’âŸ¨ Î²-Æ› vM' âŸ©
          N [ M' ]                  â€”â† âŸ¨ â†’V âŸ©
          V                         âˆ
 
fundamental-property {A} `zero âŠ¢Ïƒ = ğ’±â†’â„° {`â„•} tt
```

All STLC programs terminate.

```
terminate : âˆ€ {A}
  â†’ (M : âˆ… âŠ¢ A)
  â†’ Î£[ V âˆˆ âˆ… âŠ¢ A ] (M â€”â†  V) Ã— Value V
terminate {M} âŠ¢M
    with fundamental-property {Ïƒ = id} âŠ¢M (Î» ())
... | âŸ¨ V , âŸ¨ Mâ€”â† V , âŸ¨ vV , ğ’±V âŸ© âŸ© âŸ© =
      âŸ¨ V , âŸ¨ Mâ€”â† V , vV âŸ© âŸ©
```

